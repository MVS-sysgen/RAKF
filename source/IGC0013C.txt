         TITLE 'ESG-SECURITY  SVC 133'                                  00010012
* CPARM='XREF(SHORT),OBJ,NODECK,RENT',LPARM='RENT'            RAKF013C  00020382
*      DD DSN=SYS1.MACLIB,DISP=SHR                                      00030012
*      DD DSN=SYS1.MODGEN,DISP=SHR                                      00040012
         EJECT                                                          00050012
RAKF013C CSECT                                                          00060382
*                                                                       00070014
**********************************************************************  00080014
*                                                                    *  00090014
*    COPYRIGHT (C) 1991 BY CRAIG J. YASUNA.  ALL RIGHTS RESERVED.    *  00100014
*                                                                    *  00110014
*    THIS SOFTWARE PRODUCT OR ANY OF ITS COMPONENTS MUST NOT BE      *  00120014
*    SOLD, GIVEN, OR OTHERWISE DISTRIBUTED TO ANY OTHER COMPANY      *  00130014
*    WITHOUT THE PRIOR WRITTEN PERMISSION OF:                        *  00140014
*                                                                    *  00150014
*                                  CRAIG J. YASUNA, PRESIDENT        *  00160014
*                                  ENTERPRISE SYSTEMS GROUP          *  00170014
*                                  2 MARC COURT                      *  00180014
*                                  EDISON, NEW JERSEY 08820          *  00190014
*                                                                    *  00200014
**********************************************************************  00210014
*                                                                       00220014
         USING RAKF013C,R6         SVC ENTRY R6 = EP.                   00230382
         LR    R9,R1               SAVE ENTRY REG.                      00240008
         LR    R8,R14              SAVE RETURN REG.                     00250008
         GETMAIN RC,LV=GMLEN       GET STORAGE                          00260008
         LR    R13,R1              LOAD REGISTER 13 WITH GETMAINED PTR  00270008
         USING WORKAREA,R13        SET UP DATA DSECT ADDR.              00280008
         MVC   RACRGM,RACRREAL     move RACROUTE                        00285381
*                                                                       00360008
         L     R15,PSATOLD-PSA(0)  ADDRESS TCB                          00370382
         L     R15,TCBRBP-TCB(R15) ADDRESS RB                           00380008
         XR    R14,R14             CLEAR R14                            00390008
         ICM   R14,B'0111',RBLINKB-RBSECT(R15) PRIOR RB                 00400008
*                                                                       00430008
         LR    R10,R9              SAVE ENTRY REG.                      00440008
         ICM   R10,B'1000',=X'00'  CLEAR HIGH ORDER OF PARM ADDR.       00450008
         MVC   RACRGM+RACRLEN-RACDLEN(RACDLEN-8),0(R10) MOVE PARM       00460381
         NC    RACRGM+RACRLEN-RACDLEN+8(28),=7X'00FFFFFF'               00470008
         NC    RACRGM+RACRLEN-RACDLEN+40(8),=7X'00FFFFFF'               00480008
*                                                                       00500008
STRTSAFR LA    R5,SAFWORK          FOR SAF                              00520008
         RACROUTE REQUEST=DEFINE,WORKA=(R5),MF=(E,RACRGM)               00530008
         L     R3,RACRGM           RACDEF RETURN CODE                   00540008
         L     R4,RACRGM+4         RACDEF REASON CODE                   00550008
*                                                                       00620008
RTRNFREE FREEMAIN RC,LV=GMLEN,A=(R13)                                   00630008
         LR    R15,R3              RETURN CODE                          00640008
         LR    R0,R4               REASON CODE                          00650008
         LR    R1,R9               RETURN R1                            00660008
         BR    R8                  RETURN TO ORIG. R14                  00670008
         LTORG                                                          00680008
RACRREAL RACROUTE REQUEST=DEFINE,MF=L                                   00690008
RACRLEN  EQU   *-RACRREAL                                               00700008
RACDEF   RACDEF MF=L               THIS IS JUST FOR THE LENGTH          00710008
         DC    A(0)       INSTALLATION DATA ADDRESS (missing in RACDEF) 00713381
         DC    A(0)       ENTITY NAME ADDR FIELD    (missing in RACDEF) 00716381
RACDLEN  EQU   *-RACDEF                                                 00720008
*                                                                       00730008
WORKAREA DSECT                                                          00740008
SAVEAREA DS     18F                                                     00750008
SAFWORK  DS     XL512                                                   00760008
RACRGM   DS     XL(RACRLEN)                                             00770008
GMLEN    EQU    *-WORKAREA                                              00780008
         PRINT  NOGEN                                                   00790008
         YREGS                                                          00800008
         CVT     DSECT=YES,LIST=NO                                      00810008
         IHAPSA  DSECT=YES                                              00820380
         IKJRB   DSECT=YES                                              00830380
         IKJTCB  DSECT=YES,LIST=NO                                      00840008
         END                                                            00890008
